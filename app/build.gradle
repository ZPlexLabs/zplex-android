plugins {
    id "com.android.application"
    id "kotlin-android"
    id "androidx.navigation.safeargs.kotlin"
    id "dagger.hilt.android.plugin"
    id "kotlin-kapt"
}

android {

    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    packagingOptions {
        exclude "META-INF/DEPENDENCIES"
    }

    compileSdk 33

    defaultConfig {
        applicationId "zechs.zplex"
        minSdk 26
        targetSdk 33
        versionCode 22
        versionName "4.0"
        archivesBaseName = "${applicationId}-${versionCode}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String",
                "TMDB_API_KEY", "\"${properties.getProperty("TMDB_API_KEY")}\""

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'
            ), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

}

dependencies {

    def hilt_version = "2.44.2"
    def moshi_version = "1.13.0"
    def retrofit_version = "2.9.0"
    def room_version = "2.4.3"
    def lifecycle_version = "2.5.1"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Dagger hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Dagger hilt for navigation
    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"

    // Moshi
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"

    // Gson
    implementation "com.google.code.gson:gson:2.9.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.13.2"
    implementation("com.github.bumptech.glide:okhttp3-integration:4.12.0") {
        exclude group: 'glide-parent'
    }
    kapt "com.github.bumptech.glide:compiler:4.12.0"
    implementation 'jp.wasabeef:glide-transformations:4.3.0'

    // OkHttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.3"))
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    // Navigational Components
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.3"

    // Coroutine & Lifecycle
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Kotlin
    implementation "androidx.core:core-ktx:1.9.0"

    // Palette
    implementation "androidx.palette:palette-ktx:1.0.0"

    // AndroidX
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "com.google.android.material:material:1.8.0-beta01"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    // Tests
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
}